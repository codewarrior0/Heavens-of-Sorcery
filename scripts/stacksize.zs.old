////Imports
import crafttweaker.item.IItemStack;
import crafttweaker.oredict.IOreDict;
import crafttweaker.oredict.IOreDictEntry;

//Arrays 
//Add a block/item to an array to set its stack size to the stated number
//make sure every item except the last one in each array has a comma
//If wanting to do bigger than 8192, need to increase the limit in stackup.cfg too

var materials1024 = [
	//Ingots
	<minecraft:iron_ingot>,
	<minecraft:gold_ingot>,
	<astralsorcery:itemcraftingcomponent:1>, //starmetal
	<bewitchment:cold_iron_ingot>,
	<blue_skies:falsite_ingot>,
	<blue_skies:ventium_ingot>,
	<blue_skies:horizonite_ingot>,
	<botania:manaresource>, //manasteel, terrasteel, elementium, gaiaspirit and a whole bunch of other items
	<embers:ingot_copper>,
	<embers:ingot_lead>,
	<embers:ingot_silver>,
	<embers:ingot_dawnstone>,
	<embers:ingot_aluminum>,
	<embers:ingot_bronze>,
	<embers:ingot_electrum>,
	<embers:ingot_nickel>,
	<embers:ingot_tin>,
	<kathairis:titaniumingot>,
	<mist:niobium_ingot>,
	<naturalpledge:resource>, //thundersteel
	<naturesaura:sky_ingot>,
	<naturesaura:infused_iron>,
	<soot:ingot_antimony>,
	<thaumcraft:ingot>, //thaumium, alch brass, void metal
	<thaumic_arcana:ingot_livium>,
	<thebetweenlands:items_misc>, 
	<thebetweenlands:octine_ingot>,
	<embers:archaic_brick>,
	<embers:brick_caminite>,
	//Nuggets
	<minecraft:gold_nugget>,
	<minecraft:iron_nugget>,
	<bewitchment:cold_iron_nugget>,
	<embers:nugget_copper>,
	<embers:nugget_lead>,
	<embers:nugget_silver>,
	<embers:nugget_dawnstone>,
	<embers:nugget_aluminum>,
	<embers:nugget_bronze>,
	<embers:nugget_electrum>,
	<embers:nugget_nickel>,
	<embers:nugget_tin>,
	<extrabitmanipulation:diamond_nugget>,
	<mist:niobium_nugget>,
	<thaumcraft:nugget>,
	//gems
	<minecraft:diamond>,
	<minecraft:coal>,
	<minecraft:redstone>,
	<minecraft:quartz>,
	<thaumcraft:nugget>,
	<rustic:dust_tiny_iron>,
	<minecraft:glowstone_dust>,
	<arcaneworld:amethyst>,
	<arcaneworld:sapphire>,
	<embers:shard_ember>,
	<embers:crystal_ember>,
	<minecraft:obsidian>,
	//Ingredients
	<minecraft:paper>,
	<embers:ancient_motive_core>
] as IItemStack[];

var mobDrops512 = [
	<minecraft:rotten_flesh>,
	<minecraft:bone>,
	<minecraft:leather>,
	<minecraft:string>,
	<minecraft:blaze_rod>,
	<minecraft:web>,
	<minecraft:skull>
] as IItemStack[];

var oresAndBlocks512 = [
	<minecraft:gold_ore>,
	<minecraft:iron_ore>,
	<minecraft:coal_ore>,
	<minecraft:diamond_ore>,
	<minecraft:emerald_ore>,
	<minecraft:quartz_ore>,
	<minecraft:redstone_ore>,
	<minecraft:lapis_ore>,
	<minecraft:lapis_block>,
	<minecraft:gold_block>,
	<minecraft:iron_block>,
	<minecraft:emerald_block>,
	<minecraft:quartz_block>,
	<minecraft:coal_block>,
	<minecraft:bone_block>,
	<minecraft:slime>
] as IItemStack[];

var dict4096 = [
	<ore:logWood>,
	<ore:plankWood>,
	<ore:slabWood>,
	<ore:stairWood>,
	<ore:treeLeaves>,
	<ore:fenceWood>,
	<ore:paneGlass>
] as IOreDictEntry[];

var dict256 = [
	<ore:bookshelf>,
	<ore:chest>,
	<ore:vine>
] as IOreDictEntry[];

var stack256 = [
	<minecraft:wheat_seeds>,
	<cuisine:crops>,
	<rustic:tomato_seeds>,
	<rustic:chili_pepper_seeds>,
	<rustic:ironwoodseed>,
	<botania:grassseeds>,
	<minecraft:wheat>,
	<inspirations:sugar_cane_seeds>,
	<minecraft:reeds>,
	<minecraft:nether_wart>,
	<minecraft:sapling>,
	<minecraft:tallgrass>,
	<minecraft:deadbush>,
	<minecraft:yellow_flower>,
	<minecraft:red_flower>,
	<minecraft:red_mushroom>,
	<minecraft:brown_mushroom>,
	<minecraft:furnace>,
	<minecraft:cactus>,
	<minecraft:jukebox>,
	<minecraft:waterlily>,
	<minecraft:enchanting_table>,
	<minecraft:anvil>,
	<minecraft:double_plant>,
	<minecraft:painting>,
	<minecraft:sign>,
	<minecraft:bed>,
	<minecraft:item_frame>,
	<minecraft:flower_pot>,
	<minecraft:armor_stand>,
	<minecraft:banner>,
	<minecraft:end_crystal>,
	<minecraft:dispenser>,
	<minecraft:noteblock>,
	<minecraft:sticky_piston>,
	<minecraft:piston>,
	<minecraft:tnt>
] as IItemStack[];

var stack512 = [
	<minecraft:pumpkin>,
	<minecraft:lit_pumpkin>,
	<minecraft:melon_block>,
	<minecraft:hay_block>,
	<minecraft:nether_wart_block>,
	<minecraft:chorus_plant>,
	<minecraft:chorus_flower>
] as IItemStack[];

var stack1024 = [
	<minecraft:wool>,
	<minecraft:ice>,
	<minecraft:torch>,
	<contenttweaker:boneshard>,
	<rustic:chili_pepper>,
	<rustic:wildberries>,
	<minecraft:clay_ball>,
	<rustic:ironberries>,
	<minecraft:packed_ice>,
	<minecraft:magma>,
	<minecraft:ladder>,
	<minecraft:snow_layer>,
	<minecraft:carpet>,
	<minecraft:end_rod>
] as IItemStack[];

var stack2048 = [
	<minecraft:stained_glass>,
	<minecraft:brick_block>,
	<minecraft:clay>,
	<minecraft:snow>,
	<minecraft:glowstone>,
	<minecraft:dye:15>,
	<minecraft:sea_lantern>
] as IItemStack[];

var stack4096 = [
	<minecraft:stone_slab>,
	<minecraft:stone_stairs>,
	<minecraft:stonebrick>,
	<minecraft:brick_stairs>,
	<minecraft:stone_brick_stairs>,
	<minecraft:nether_brick>,
	<minecraft:nether_brick_stairs>,
	<minecraft:stick>,
	<minecraft:sandstone_stairs>,
	<minecraft:quartz_stairs>,
	<minecraft:prismarine>,
	<minecraft:red_sandstone_stairs>,
	<minecraft:stone_slab2>,
	<minecraft:purpur_stairs>,
	<minecraft:purpur_slab>,
	<minecraft:iron_bars>,
	<minecraft:nether_brick_fence>,
	<minecraft:cobblestone_wall>
] as IItemStack[];

var stack8192 = [
	<minecraft:stone>,
	<minecraft:dirt>,
	<minecraft:cobblestone>,
	<minecraft:sand>,
	<minecraft:glass>,
	<minecraft:gravel>,
	<minecraft:mossy_cobblestone>,
	<minecraft:netherrack>,
	<minecraft:soul_sand>,
	<minecraft:end_stone>,
	<minecraft:grass>,
	<minecraft:mycelium>,
	<quark:basalt>,
	<botania:biomestonea>,
	<minecraft:stained_hardened_clay>,
	<minecraft:hardened_clay>,
	<minecraft:purpur_block>,
	<minecraft:purpur_pillar>,
	<minecraft:end_bricks>,
	<minecraft:sandstone>,
	<minecraft:red_sandstone>,
	<minecraft:red_nether_brick>,
	<minecraft:concrete>,
	<minecraft:concrete_powder>,
	<minecraft:white_glazed_terracotta>,
	<minecraft:orange_glazed_terracotta>,
	<minecraft:magenta_glazed_terracotta>,
	<minecraft:yellow_glazed_terracotta>,
	<minecraft:light_blue_glazed_terracotta>,
	<minecraft:lime_glazed_terracotta>,
	<minecraft:pink_glazed_terracotta>,
	<minecraft:gray_glazed_terracotta>,
	<minecraft:silver_glazed_terracotta>,
	<minecraft:cyan_glazed_terracotta>,
	<minecraft:purple_glazed_terracotta>,
	<minecraft:blue_glazed_terracotta>,
	<minecraft:brown_glazed_terracotta>,
	<minecraft:green_glazed_terracotta>,
	<minecraft:red_glazed_terracotta>,
	<minecraft:black_glazed_terracotta>
] as IItemStack[];

for stuffDict in dict256 {
	for stuff in stuffDict.items {
		stack256 += stuff;
	}
}

for oreBlock in oresAndBlocks512 {
	stack512 += oreBlock;
}

for mobdrops in mobDrops512 {
	stack512 += mobdrops;
}

for material in materials1024 {
	stack1024 += material;
}

for woodDict in dict4096 {
	for dictEntry in woodDict.items {
		stack4096 += dictEntry;
	}
}

var stackTweakMap = {} as IItemStack[][int];
stackTweakMap[256] = stack256;
stackTweakMap[512] = stack512;
stackTweakMap[1024] = stack1024;
stackTweakMap[2048] = stack2048;
stackTweakMap[4096] = stack4096;
stackTweakMap[8192] = stack8192;


for stackSize, items in stackTweakMap {
	for i in items {
		i.maxStackSize = stackSize;
	}
}
